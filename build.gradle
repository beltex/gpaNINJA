// CSS prep

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-css-plugin:1.8.3'
    }
}

apply plugin: 'css'

css.source {
    prep {
        css {
            srcDir "WebContent/res/"
            include "*.css"
            exclude "*.min.css"
        }
    }
}

// Specify a collection of files to be combined, then minified
combineCss {
    source = css.source.prep.css.files
    dest = "${buildDir}/all.css"
}

minifyCss {
    source = combineCss
    dest = "${buildDir}/tmp/css/gpaNINJA.css"
}

// WAR file build

apply plugin: 'war'
apply plugin: 'eclipse-wtp'

war {
    dependsOn 'minifyCss'
    exclude 'WebContent/res/*.css'

  from ('build/tmp/css') {
    include '**/*.css'
    into 'res'
  }
}

sourceCompatibility = 1.7
war.archiveName = 'gpaNINJA.war'
project.webAppDirName = 'WebContent'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    runtime 'javax.servlet:jstl:1.1.2'
    testCompile 'junit:junit:4.11'
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
}
