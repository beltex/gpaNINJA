apply plugin: 'css'
apply plugin: 'war'
apply plugin: 'findbugs'
apply plugin: 'eclipse-wtp'

version = '1.0.0'
description = 'York University automatic GPA calculator'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

war.archiveName = 'gpaNINJA.war'
project.webAppDirName = 'WebContent'

repositories { mavenCentral() }

dependencies {
  compile         'org.jsoup:jsoup:1.7.3'
  providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
  runtime         'javax.servlet:jstl:1.1.2'
  testCompile     'junit:junit:4.11'
}


/*
 * CSS prep - specify a collection of files to be combined, then minified
 */


buildscript {
  repositories { mavenCentral() }
  dependencies {
    classpath 'com.eriwen:gradle-css-plugin:1.11.1'
  }
}

css.source {
  prep {
    css {
      srcDir "WebContent/css/"
      include "*.css"
      exclude "*.min.css"
    }
  }
}

combineCss {
  source = css.source.prep.css.files
  dest = "${buildDir}/all.css"
}

minifyCss {
  source = combineCss
  dest = "${buildDir}/tmp/css/gpaNINJA.css"
}


/*
 * FindBugs
 */


tasks.withType(FindBugs) {
  findbugsMain.onlyIf {project.properties['findbugs'].toBoolean()}
  findbugsTest.onlyIf {project.properties['findbugs'].toBoolean()}

  ignoreFailures = true
  reports {
    xml.enabled = false
    html.enabled = true
  }
}


/*
 * WAR file build
 */


war {
  dependsOn 'minifyCss'
  exclude 'WebContent/css/*.css'

  from ('build/tmp/css') {
    include '**/*.css'
    into 'css'
  }
}
